String getConfig(String name, String theDefault) {
    final Provider<String> variable = providers.environmentVariable(name)
    if (variable.isPresent()) {
        return variable.orElse(theDefault).get()
    }
    final Object value = project.findProperty(name)
    if (null == value) {
        return theDefault
    }
    return value.toString()
}

String getWorkflowName() {
    getConfig("PROJECT_WORKFLOW", "unknown")
}

boolean doingDeveloperRelease() {
    "developer-release" == getWorkflowName()
}

boolean doingMainRelease() {
    "main-release" == getWorkflowName() && !isSnapshot()
}

boolean isSnapshot() {
    (version as String).endsWith("SNAPSHOT")
}

boolean doingSnapshotRelease() {
    "main-release" == getWorkflowName()  && isSnapshot()
}

boolean doingRelease() {
    doingDeveloperRelease()|| doingMainRelease() || doingSnapshotRelease()
}

boolean doingSigning() {
    doingRelease()
}

if (doingSigning()) {
    println "Doing signing"
    apply plugin: 'io.github.jonloucks.signing-kit'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Concurrency'
                description = 'Concurrency a core java library for concurrency.'
                        ' dependency inversion, Dependency injection, Inversion of Control,' +
                        ' Solid Principles: Single responsibility, Open-closed, Liskov substitution, Interface segregation, Dependency inversion.' +
                        ' OpenSSF Best Practices. Java Module Support. Strong data encapsulation.'
                url = 'https://github.com/jonloucks/concurrency'
                inceptionYear = '2025'
                organization {
                    name = 'io.github.jonloucks'
                    url = 'https://github.com/jonloucks'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/jonloucks/concurrency?tab=MIT-1-ov-file#MIT-1-ov-file'
                    }
                }
                developers {
                    developer {
                        id = 'jonloucks'
                        name = 'Jon Loucks'
                        email = 'j.loucks@comcast.net'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/jonloucks/concurrency.git'
                    developerConnection = 'scm:git:ssh://github.com:jonloucks/concurrency.git'
                    url = 'https://github.com/jonloucks/concurrency'
                }
            }
        }
    }
}

if (doingSigning()) {
    signing {
        sign publishing.publications.mavenJava
    }
}